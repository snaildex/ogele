#version 430

layout( local_size_x = 1 ) in;

struct Triangle {
	vec4 Position[3];
	vec4 Normal[3];
	vec4 TriNormal;
	vec4 NormDot;
	vec4 Domain;
	vec4 VertexData[3];
};

layout(std430, binding = 0) buffer PosBuffer { vec4 Pos[]; };
layout(std430, binding = 1) buffer NormBuffer { vec4 Norm[]; };
layout(std430, binding = 2) buffer IndexBuffer { uvec4 Index[]; };
layout(std430, binding = 3) buffer TrisBuffer { Triangle Tris[]; };

uint GetIndex(uint i) { return Index[i/4][i%4]; }

float CalculateSphere(vec3 a, vec3 b, vec3 n) {
	vec3 d = (b-a) / 2;
	return dot(-n, normalize(d)) / length(d);
}

float AngleBetween(vec3 a, vec3 b) { return acos(dot(normalize(a), normalize(b))); }

void main()
{
	uint idx = gl_GlobalInvocationID.x;
	Triangle tri;
	for(int i=0; i<3; ++i) {
		tri.Position[i] = Pos[GetIndex(idx * 3 + i)];
		tri.Normal[i] = normalize(Norm[GetIndex(idx * 3 + i)]);
	}
	tri.TriNormal.xyz = normalize(cross(tri.Position[1].xyz-tri.Position[0].xyz,tri.Position[2].xyz-tri.Position[0].xyz));
	tri.NormDot.xyz = vec3(dot(tri.TriNormal.xyz,tri.Normal[0].xyz), dot(tri.TriNormal.xyz,tri.Normal[1].xyz), dot(tri.TriNormal.xyz,tri.Normal[2].xyz));

	float a=distance(tri.Position[1].xyz, tri.Position[2].xyz);
	float b=distance(tri.Position[0].xyz, tri.Position[2].xyz);
	float c=distance(tri.Position[0].xyz, tri.Position[1].xyz);
	vec3 bc;
	bc.x=a*a*(b*b+c*c-a*a);
	bc.y=b*b*(a*a+c*c-b*b);
	bc.z=c*c*(b*b+a*a-c*c);
	float sum=bc.x+bc.y+bc.z;
	bc/=sum;
	tri.Domain.xyz=tri.Position[0].xyz*bc.x+tri.Position[1].xyz*bc.y+tri.Position[2].xyz*bc.z;
	tri.Domain.w=distance(tri.Domain.xyz,tri.Position[0].xyz);
	tri.VertexData[0].x=CalculateSphere(tri.Position[0].xyz,tri.Position[1].xyz,tri.Normal[0].xyz);
	tri.VertexData[0].y=CalculateSphere(tri.Position[0].xyz,tri.Position[2].xyz,tri.Normal[0].xyz);
	tri.VertexData[1].x=CalculateSphere(tri.Position[1].xyz,tri.Position[0].xyz,tri.Normal[1].xyz);
	tri.VertexData[1].y=CalculateSphere(tri.Position[1].xyz,tri.Position[2].xyz,tri.Normal[1].xyz);
	tri.VertexData[2].x=CalculateSphere(tri.Position[2].xyz,tri.Position[0].xyz,tri.Normal[2].xyz);
	tri.VertexData[2].y=CalculateSphere(tri.Position[2].xyz,tri.Position[1].xyz,tri.Normal[2].xyz);
	tri.VertexData[0].z=AngleBetween(tri.Position[1].xyz-tri.Position[0].xyz,tri.Position[2].xyz-tri.Position[0].xyz);
	tri.VertexData[1].z=AngleBetween(tri.Position[2].xyz-tri.Position[1].xyz,tri.Position[0].xyz-tri.Position[1].xyz);
	tri.VertexData[2].z=AngleBetween(tri.Position[0].xyz-tri.Position[2].xyz,tri.Position[1].xyz-tri.Position[2].xyz);

	Tris[idx] = tri;
}